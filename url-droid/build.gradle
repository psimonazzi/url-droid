apply plugin: 'java'

sourceCompatibility = "1.6"
targetCompatibility = "1.6"
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

version = '2.4.6'

ext.buildTimestamp = new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss").format(new Date())


repositories {
    jcenter()
}


dependencies {
    testCompile 'junit:junit:4.12'
}

ext.appManifest = manifest {
    attributes(
        "Application-Version": version,
        "Implementation-Title": "it.idsolutions.util.HttpClient",
        "Implementation-Version": version,
        "Build-Timestamp": project.ext.buildTimestamp,
        "Min-Java-Version": sourceCompatibility,
        'Source-Compatibility': project.sourceCompatibility,
        'Target-Compatibility': project.targetCompatibility
    )
}
tasks.withType(Jar) { manifest = project.manifest { from appManifest } }


task sourcesJar(type: Jar, dependsOn: classes,
    description: 'Create a jar that also includes sources') {
    classifier = 'sources'
    
    if (gradle.gradleVersion < '4.0') from sourceSets.main.output.classesDir
    else from sourceSets.main.output.classesDirs
    from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn: javadoc,
    description: 'Create javadoc jar') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


// build additional artifacts
artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:cast" << "-Xlint:finally" << "-Xlint:overrides" << "-Xlint:path" << "-Xlint:deprecation"
}


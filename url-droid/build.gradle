apply plugin: 'java'

sourceCompatibility = "1.6"
targetCompatibility = "1.6"
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

version = '2.0.0'

repositories {
  mavenCentral()
}

dependencies {
  testCompile 'junit:junit:4.11'
}


task sourcesAndClassesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
  from sourceSets.main.output.classesDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesAndClassesJar
  //archives javadocJar
}

Date buildTimeAndDate = new Date()
ext {
  buildTimestamp = new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(buildTimeAndDate)
}
sourceSets {
  main {
    // also put resources in classes dir
    output.resourcesDir = output.classesDir
  }
}
processResources {
  from(sourceSets.main.resources.srcDirs) {
    include '**/app.properties'
    filter(org.apache.tools.ant.filters.ReplaceTokens,
           tokens: [
             version: project.version,
             gradleVersion: project.gradle.gradleVersion,
             buildTimestamp: project.ext.buildTimestamp
           ])
  }
}


task wrapper(type: Wrapper,
             description: 'Creates and deploys the Gradle wrapper to the current directory.') {
  gradleVersion = '2.2'
}
